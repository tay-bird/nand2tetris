// This file is part of the materials accompanying the book
// "The Elements of Computing Systems" by Nisan and Schocken,
// MIT Press. Book site: www.idc.ac.il/tecs
// File name: projects/03/b/RAM512.hdl

/**
 * Memory of 512 registers, each 16 bit-wide. Out holds the value
 * stored at the memory location specified by address. If load==1, then
 * the in value is loaded into the memory location specified by address
 * (the loaded value will be emitted to out from the next time step onward).
 */

CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:
    DMux8Way(
      in=load,
      a=LoadR0,
      b=LoadR1,
      c=LoadR2,
      d=LoadR3,
      e=LoadR4,
      f=LoadR5,
      g=LoadR6,
      h=LoadR7,
      sel=address[6..8]);

    RAM64(in=in, load=LoadR0, address=address[0..5], out=ROut0);
    RAM64(in=in, load=LoadR1, address=address[0..5], out=ROut1);
    RAM64(in=in, load=LoadR2, address=address[0..5], out=ROut2);
    RAM64(in=in, load=LoadR3, address=address[0..5], out=ROut3);
    RAM64(in=in, load=LoadR4, address=address[0..5], out=ROut4);
    RAM64(in=in, load=LoadR5, address=address[0..5], out=ROut5);
    RAM64(in=in, load=LoadR6, address=address[0..5], out=ROut6);
    RAM64(in=in, load=LoadR7, address=address[0..5], out=ROut7);

    Mux8Way16(
      a=ROut0,
      b=ROut1,
      c=ROut2,
      d=ROut3,
      e=ROut4,
      f=ROut5,
      g=ROut6,
      h=ROut7,
      sel=address[6..8],
      out=out);
}
