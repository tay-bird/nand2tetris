class ConwayGame {

  field ConwayWorld flip;
  field ConwayWorld flop;

  field int scale;
  field int seed;
  field int size;

  constructor ConwayGame new() {
    let scale = 10;
    return this;
  }

  method void dispose() {
    do flip.dispose();
    do flop.dispose();
    do Memory.deAlloc(this);
    return;
  }

  method void run() {
    var char key;

    do runSplash();

    let flip = ConwayWorld.new(scale);
    let flop = ConwayWorld.new(scale);

    if (seed = 0) {
        do runCreate();
    }
    else {
        do flip.randomize(seed);
    }

    while (~(key = 140)) {  // esc key
      let key = Keyboard.keyPressed();
      do runConway();
    }
    return;
  }

  method void runConway() {
    do flip.drawWorld();
    do flop.update(flip);
    do flop.drawWorld();
    do flip.update(flop);

    return;
  }

  method void runCreate() {
      var Cursor cursor;
      var boolean drawCommand, exit;
      var int direction, key, x, y;

      let exit = false;
      let cursor = Cursor.new(scale);

      do cursor.draw();
      do Sys.wait(500);

      while (~exit) {
          let direction = 0;
          let drawCommand = false;
          let key = Keyboard.keyPressed();

          if (key = 131) { let direction = 1; }   // up arrow
          if (key = 133) { let direction = 2; }   // down arrow
          if (key = 130) { let direction = 3; }   // left arrow
          if (key = 132) { let direction = 4; }   // right arrow
          if (key = 140) { let exit = true; }     // esc key

          if (direction > 0) {
              let x = cursor.getX();
              let y = cursor.getY();

              do flip.drawCell(x, y);
              do cursor.move(direction);
          }

          if (key = 128) {
              let x = cursor.getX();
              let y = cursor.getY();
              do flip.set(x, y);
          }

          while (~(key = 0)) {
              let key = Keyboard.keyPressed();
          }  // wait for key release
      }

      return;
  }

  method void runSplash() {
    var int key;
    var String keyString;

    do Output.println();
    do Output.println();
    do Output.println();
    do Output.println();
    do Output.println();
    do Output.println();
    do Output.println();
    do Output.println();
    do Output.println();
    do Output.println();
    do Output.printString("                      Conway's Game of Life");


    do Output.println();
    do Output.println();
    do Output.printString("                    Press Enter for draw mode");

    do Output.println();
    do Output.printString("                Press any other key for seed mode");

    while (key = 0) {
      let key = Keyboard.keyPressed();
    }

    if (key = 128) {
        let seed = 0;
    }
    else {
        let seed = key;
    }

    do Screen.clearScreen();

    return;
  }
}
